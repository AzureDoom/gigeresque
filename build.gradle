buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://maven.parchmentmc.org' }
    }
}

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archivesBaseName
version = project.modVersion
group = project.mavenGroup

repositories {
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com" }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'https://maven.gegy.dev' }
    maven { url 'https://maven.enginehub.org/repo/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings( loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.14@zip")
	})
    modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.19:3.1.25"
	modImplementation ("com.terraformersmc:modmenu:4.0.6")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:9.1.520") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:9.1.520") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("dev.architectury:architectury-fabric:6.0.37") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.shedaniel.cloth:cloth-config-fabric:8.0.75") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation 'com.sk89q.worldedit:worldedit-fabric-mc1.19.2:7.2.12'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

loom {
	accessWidenerPath = file("src/main/resources/gigeresque.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "535416"
        	releaseType = "release"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.19.1")
			addGameVersion("1.19.2")
			addGameVersion("Fabric")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'geckolib'  
            	optionalDependency 'modmenu'
            	optionalDependency 'dripsounds-fabric'
            	optionalDependency 'sound-physics-remastered'
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'QyVF9oeo' 
		versionNumber = project.mod_version 
		versionName = 'Fabric 1.19.2'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.19.2']
		loaders = ['fabric']
		dependencies { 
        	required.project "geckolib"
        	required.project "fabric-api"
            optional.project "modmenu"
            optional.project "dripsounds-fabric"
            optional.project "sound-physics-remastered"
   		}
	}
}